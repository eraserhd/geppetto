(ns net.eraserhead.geppetto.gcode-test
  (:require
   [clojure.test :refer [deftest are]]
   [net.eraserhead.geppetto.gcode :as gcode]))

(deftest t-parse-line
  (are [line tree] (= tree (gcode/parse-line line))
    "N105"             [[::gcode/line-number 105.0]]
    "n105"             [[::gcode/line-number 105.0]]
    "n010"             [[::gcode/line-number 10.0]]
    " n105"            [[::gcode/line-number 105.0]]
    "\t n105"          [[::gcode/line-number 105.0]]
    "n105 \t\t"        [[::gcode/line-number 105.0]]
    "n 105"            [[::gcode/line-number 105.0]]
    "n1 2\t3  4 5"     [[::gcode/line-number 12345.0]]
    "G-1.4"            [[::gcode/G -1.4]]
    "g-1.4"            [[::gcode/G -1.4]]
    "F2400"            [[::gcode/F 2400.0]]
    "f2400"            [[::gcode/F 2400.0]]
    "f-4"              [[::gcode/F -4.0]]
    "f+4"              [[::gcode/F +4.0]]
    "f.4"              [[::gcode/F 0.4]]
    "f-.4"             [[::gcode/F -0.4]]
    "f+.4"             [[::gcode/F +0.4]]
    "f -4"             [[::gcode/F -4.0]]
    "f+ .4"            [[::gcode/F 0.4]]
    "f+0 .4"           [[::gcode/F 0.4]]
    "g1x4.2f99z6"      [[::gcode/G 1.0] [::gcode/X 4.2] [::gcode/F 99.0] [::gcode/Z 6.0]]
    "g1 x4.2 f99 z6"   [[::gcode/G 1.0] [::gcode/X 4.2] [::gcode/F 99.0] [::gcode/Z 6.0]]
    "f[4]"             '[[::gcode/F 4.0]]
    "f[4 + 7]"         '[[::gcode/F (+ 4.0 7.0)]]
    "f[4 + 7 + 9]"     '[[::gcode/F (+ (+ 4.0 7.0) 9.0)]]
    "f[4-7+8-6]"       '[[::gcode/F (- (+ (- 4.0 7.0) 8.0) 6.0)]]
    "f[4*9]"           '[[::gcode/F (* 4.0 9.0)]]
    "f[4-6*9]"         '[[::gcode/F (- 4.0 (* 6.0 9.0))]]
    "f[4*6/9]"         '[[::gcode/F (/ (* 4.0 6.0) 9.0)]]
    "f[1**2**3]"       '[[::gcode/F (** 1.0 (** 2.0 3.0))]]
    "f[[3+6]*4]"       '[[::gcode/F (* (+ 3.0 6.0) 4.0)]]
    "f[abs[-6]]"       '[[::gcode/F (abs -6.0)]]
    "f[atan[6]/[4]]"   '[[::gcode/F (atan 6.0 4.0)]]
    "f#1"              '[[::gcode/F (parameter 1.0)]]
    "f##1"             '[[::gcode/F (parameter (parameter 1.0))]]
    "#22=32.4"         '[[::gcode/parameter= 22.0 32.4]]
    "#[1+2]=[3+4]"     '[[::gcode/parameter= (+ 1.0 2.0) (+ 3.0 4.0)]]
    "##2=3"            '[[::gcode/parameter= (parameter 2.0) 3.0]]
    "(Test comment)"   '[[::gcode/comment "Test comment"]]
    "(MSG,Hello TH)"   '[[::gcode/message "Hello TH"]]
    "(m S\tg ,Hel TH)" '[[::gcode/message "Hel TH"]]

    ;; linuxcnc extensions
    "n100.74"          '[[::gcode/line-number 100.74]]
    "u62.4 v1.6 w11.4" '[[::gcode/U 62.4] [::gcode/V 1.6] [::gcode/W 11.4]]))
